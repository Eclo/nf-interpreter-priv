INCLUDE(${CMAKE_CURRENT_LIST_DIR}/FindSTMFilesUtil.cmake)

STRING(TOLOWER ${STM32_SERIES} STM32_SERIES_LOWER)

STM32_LOCATE_STDPERIPH_REL_HEADERS()

SET(STDPERIPH_HEADERS ${STM32_STDPERIPH_REL_HEADERS_${STM32_SERIES}})

STRING(TOLOWER ${STM32_SERIES} STM32_SERIES_LOWER)
IF(NOT STM32_CHIP_TYPE)
    FOREACH(CHIP_TYPE ${STM32_CHIP_TYPES})
        STRING(TOLOWER ${CHIP_TYPE} CHIP_TYPE_LOWER)
        LIST(APPEND STDPERIPH_FIND_LIBS stdperiph_${STM32_SERIES_LOWER}_${CHIP_TYPE_LOWER})
    ENDFOREACH()    
ELSE()
    STRING(TOLOWER ${STM32_CHIP_TYPE} STM32_CHIP_TYPE_LOWER)
    SET(STDPERIPH_FIND_LIBS stdperiph_${STM32_SERIES_LOWER}_${STM32_CHIP_TYPE_LOWER})
ENDIF()

FIND_PATH(StdPeriphLib_INCLUDE_DIR ${STDPERIPH_HEADERS}
    PATH_SUFFIXES include stm32${STM32_SERIES_LOWER}
)
# Locating STM32 HAL library
IF(NOT StdPeriphLib_INCLUDE_DIR)
    FIND_PATH(StdPeriphLib_INCLUDE_DIR ${STM32_STDPERIPH_REL_HEADERS_HAL_${STM32_SERIES}}
        PATH_SUFFIXES include stm32${STM32_SERIES_LOWER}
    )
ENDIF()

FOREACH(STDPERIPH_LIB_NAME ${STDPERIPH_FIND_LIBS})
    SET(StdPeriphLib_LIBRARY STDPERIPH_LIBRARY-NOTFOUND)
    FIND_LIBRARY(StdPeriphLib_LIBRARY
        NAMES ${STDPERIPH_LIB_NAME}
        PATH_SUFFIXES lib
    )
    LIST(APPEND StdPeriphLib_LIBRARIES ${StdPeriphLib_LIBRARY})
ENDFOREACH()

INCLUDE(FindPackageHandleStandardArgs)

FIND_PACKAGE_HANDLE_STANDARD_ARGS(StdPeriphLib DEFAULT_MSG StdPeriphLib_LIBRARIES StdPeriphLib_INCLUDE_DIR) 
